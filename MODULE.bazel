bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_cc", version = "0.2.16")
bazel_dep(name = "toolchains_llvm", version = "1.6.0")
bazel_dep(name = "rules_rust", version = "0.68.1")

git_override(
    module_name = "toolchains_llvm",
    remote = "https://github.com/ArchangelX360/toolchains_llvm", # Add Windows support, this is a temporary fork until this is merged https://github.com/bazel-contrib/toolchains_llvm/pull/642
    commit = "c8f523d554bbb2e3e8dbf9402f1138d143a8dd62",
)

git_override(
    module_name = "rules_rust",
    remote = "https://github.com/bazelbuild/rules_rust",
    commit = "cdaf15f5796e3e934b074526272823284bbaed01",
    patches = [
        "//:rules_rust-lib_include.patch", # Fix `cc` crate compilation in `build.rs` that calls it
    ],
    patch_strip = 1,
)

local_archive_ext = use_extension("//:defs.bzl", "local_archive_ext")
local_archive_ext.archive(
    # see `sysroots/README.md` to understand how this sysroot is built
    name = "sysroot-darwin",
    build_file = "@//sysroots:BUILD.darwin",
    src = "//sysroots:sysroot-macos-SDK_15.5-5185e391ddaec47fc3c03b0bd85082b1.tar.gz",
)
local_archive_ext.archive(
    # see `sysroots/README.md` to understand how this sysroot is built
    name = "sysroot-windows_aarch64",
    build_file = "@//sysroots:BUILD.windows_aarch64",
    src = "//sysroots:sysroot-windows_aarch64-MSVC_14.50.35717-SDK_10.0.22621.0-6E92FED8A6D11F344CEB8F5FF4CC5F31.tar.gz",
)
local_archive_ext.archive(
    # see `sysroots/README.md` to understand how this sysroot is built
    name = "sysroot-windows_x86_64",
    build_file = "@//sysroots:BUILD.windows_x86_64",
    src = "//sysroots:sysroot-windows_x86_64-MSVC_14.50.35717-SDK_10.0.22621.0-6E92FED8A6D11F344CEB8F5FF4CC5F31.tar.gz",
)
local_archive_ext.archive(
    # see `sysroots/README.md` to understand how this sysroot is built
    name = "sysroot-linux-aarch64",
    build_file = "@//sysroots:BUILD.linux_aarch64",
    src = "//sysroots:sysroot-linux_aarch64_3.3.0.65682.tar.zst",
)
local_archive_ext.archive(
    # see `sysroots/README.md` to understand how this sysroot is built
    name = "sysroot-linux-x86_64",
    build_file = "@//sysroots:BUILD.linux_x86_64",
    src = "//sysroots:sysroot-linux_x86_64_3.3.0.65682.tar.zst",
)
use_repo(
    local_archive_ext,
    "sysroot-darwin",
    "sysroot-windows_aarch64",
    "sysroot-windows_x86_64",
    "sysroot-linux-aarch64",
    "sysroot-linux-x86_64",
)

llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm")
llvm.toolchain(
    name = "llvm_toolchain",
    llvm_version = "20.1.3",
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot-darwin//:sysroot",
    targets = [
        "darwin-aarch64",
        "darwin-x86_64",
    ],
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot-windows_aarch64//:sysroot",
    targets = ["windows-aarch64"],
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot-windows_x86_64//:sysroot",
    targets = ["windows-x86_64"],
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot-linux-aarch64//:sysroot",
    targets = ["linux-aarch64"],
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot-linux-x86_64//:sysroot",
    targets = ["linux-x86_64"],
)
llvm.extra_target_compatible_with(
    name = "llvm_toolchain",
    constraints = [
        "@//native/libc:glibc",
    ],
)
use_repo(llvm, "llvm_toolchain", "llvm_toolchain_llvm")
register_toolchains("@llvm_toolchain//:all")

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
RUST_TARGETS = [
    "aarch64-apple-darwin",
    "x86_64-apple-darwin",
    "aarch64-pc-windows-msvc",
    "x86_64-pc-windows-msvc",
    "aarch64-unknown-linux-gnu",
    "x86_64-unknown-linux-gnu",
]
rust.toolchain(
    edition = "2021",
    extra_target_triples = RUST_TARGETS,
    versions = ["1.89.0"],
)
use_repo(rust, "rust_toolchains")
register_toolchains("@rust_toolchains//:all")

crate = use_extension("@rules_rust//crate_universe:extensions.bzl", "crate")
crate.from_cargo(
    name = "rust_crates",
    cargo_config = "//rust:.cargo/config.toml",
    cargo_lockfile = "//rust:Cargo.lock",
    lockfile = "//rust:cargo-bazel-lock.json",
    manifests = ["//rust:Cargo.toml"],
    supported_platform_triples = RUST_TARGETS,
)
use_repo(crate, "rust_crates")
