bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_cc", version = "0.2.16")
bazel_dep(name = "toolchains_llvm", version = "1.6.0")
bazel_dep(name = "rules_rust", version = "0.68.1")

git_override(
    module_name = "toolchains_llvm",
    commit = "c8f523d554bbb2e3e8dbf9402f1138d143a8dd62",
    remote = "https://github.com/ArchangelX360/toolchains_llvm",  # Add Windows support, this is a temporary fork until this is merged https://github.com/bazel-contrib/toolchains_llvm/pull/642
)

single_version_override(
    module_name = "rules_rust",
    patch_strip = 1,
    patches = [
        "//:rules_rust-musl.patch",  # forces `musl` target to be used by default, TODO: to be removed
        "//:rules_rust-lib_include.patch",  # Fix `cc` crate compilation in `build.rs` that calls it
        "//:rules_rust-verbose.patch",  # Increase verbosity
    ],
)

local_archive_ext = use_extension("//:defs.bzl", "local_archive_ext")
local_archive_ext.archive(
    # see `sysroots/README.md` to understand how this sysroot is built
    name = "sysroot-windows_aarch64",
    src = "//sysroots:sysroot-windows_aarch64-MSVC_14.50.35717-SDK_10.0.22621.0-6E92FED8A6D11F344CEB8F5FF4CC5F31.tar.gz",
    build_file = "@//sysroots:BUILD.windows_aarch64",
)
local_archive_ext.archive(
    # see `sysroots/README.md` to understand how this sysroot is built
    name = "sysroot-windows_x86_64",
    src = "//sysroots:sysroot-windows_x86_64-MSVC_14.50.35717-SDK_10.0.22621.0-6E92FED8A6D11F344CEB8F5FF4CC5F31.tar.gz",
    build_file = "@//sysroots:BUILD.windows_x86_64",
)
use_repo(
    local_archive_ext,
    "sysroot-windows_aarch64",
    "sysroot-windows_x86_64",
)

bazel_dep(name = "toolchains_llvm_bootstrapped", version = "0.3.1")
archive_override(
    module_name = "toolchains_llvm_bootstrapped",
    sha256 = "b1f855ef97d617f0f9b6b617e7e25aa3d47c08cb557e7c9f9e0eaf3d9231779b",
    strip_prefix = "toolchains_llvm_bootstrapped-c1537dd81d56a51affb55c3fdecb913d495e5d27",
    url = "https://github.com/cerisier/toolchains_llvm_bootstrapped/archive/c1537dd81d56a51affb55c3fdecb913d495e5d27.zip",
)

register_toolchains(
    "@toolchains_llvm_bootstrapped//toolchain:all",
)

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")

RUST_TARGETS = [
    "aarch64-apple-darwin",
    "x86_64-apple-darwin",
    "aarch64-pc-windows-msvc",
    "x86_64-pc-windows-msvc",
    "aarch64-unknown-linux-musl",
    "x86_64-unknown-linux-musl",
]

rust.toolchain(
    edition = "2021",
    extra_target_triples = RUST_TARGETS,
    versions = ["1.89.0"],
)
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

crate = use_extension("@rules_rust//crate_universe:extensions.bzl", "crate")
crate.from_cargo(
    name = "rust_crates",
    cargo_config = "//rust:.cargo/config.toml",
    cargo_lockfile = "//rust:Cargo.lock",
    lockfile = "//rust:cargo-bazel-lock.json",
    manifests = ["//rust:Cargo.toml"],
    supported_platform_triples = RUST_TARGETS,
)
use_repo(crate, "rust_crates")
